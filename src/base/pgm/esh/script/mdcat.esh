/*
:*:	concatenate data cubes
:de:	Datenmatrizen zusammenfassen

Copyright (C) 1997, 2001 Erich Frühstück
This file is part of EFEU.

EFEU is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public
License as published by the Free Software Foundation; either
version 2 of the License, or (at your option) any later version.

EFEU is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty
of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with EFEU; see the file COPYING.
If not, write to the Free Software Foundation, Inc.,
59 Temple Place, Suite 330, Boston, MA 02111-1307, USA.
*/

PrintListDelim = "\t";

pconfig !
#if	Lang == "de"
Ident = "Datenmatrizen zusammenfassen"
#else
Ident = "concatenate data cubes"
#endif
Version = "2"
Copyright = "(c) 1997, 2001 Erich Frühstück"
PrintMode = true
Name = X

T:title | Title
	:*:set title of output file
	:de:Titel der Ausgabedatei setzen
n:name | Name
	:*:name of axis, default {Name}
	:de:Achsenbezeichner, Vorgabe {Name}
*file(s) | 
	:*:input file(s)
	:de:Eingabedatei(en)
o:out | Output; PrintMode = false
	:*:output file
	:de:Name der Ausgabedatei
!

List_t list;
str label = Name;
str delim = "=";
str name, fname;

for (int i = 1; i < argc; i++)
{
	int idx = index(argv[i], '=')

	if	(argv[i][idx] == '=')
	{
		fname = substr(argv[i], idx + 1);
		name = substr(argv[i], 0, idx);
	}
	else	name = fname = argv[i];

	name = substr(name, 0, index(name, '.'));
	label += sprintf("%s%s", delim, name);
	list = catlist(list, mdload(fname));
	delim = ",";
}

mdmat md = mdcat(label, list);

if	(Title)		md.title = Title;

if	(flagres("PrintMode"))
	md.mark("#-1").print(iostd, "nohead");
else	md.save(Output);

free(md);
