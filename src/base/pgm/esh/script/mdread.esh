Ident = "Datenmatrix lesen";
Copyright = "(c) 1997 Erich Frühstück, A-1090 Wien, Währinger Straße 64/6";
Version = "2";

bool PrintMode = true;
bool Reduce = false;
bool SCFlag = false;
str Title = NULL;
str Input = NULL;
str Output = NULL;
str SelDef = NULL;
str VarDef = NULL;
str Flags = NULL;
str locale = NULL;

PrintListDelim = "\t";

void addflag(str x)
	Flags = paste(" ", Flags, x);

PgmOpt("x name(n)", "addflag(\"x=\"+#1)", "Spaltenachsen")
PgmOpt("y name(n)", "addflag(\"y=\"+#1)", "Zeilenachsen")
PgmOpt("t type", "addflag(\"type=\"+#1)", "Datentype")
PgmOpt("m", "addflag(\"magic\")", "Test auf Kennung")
PgmOpt("de", "locale=\"de\"", "Deutsches Zahlenformat")
PgmOpt("us", "locale=\"us\"", "Amerikanisches Zahlenformat")
PgmOpt("sc", "SCFlag=true", "SC-Spreadsheet konvertieren")
PgmOpt("r", "Reduce=true", "Entfernen von singulären Achsen")
PgmOpt("v [name=]var", "VarDef=paste(\" \", VarDef, #1)", "Variablenselektion")
PgmOpt("T titel", "Title=#1", "Titel der Ausgabedatei")
PgmArg("ein", "Input=#1", "Name der Eingabedatei")
PgmOptArg("aus", "Output=#1;PrintMode=false", "Name der Ausgabedatei")
PgmXArg("*=*", "SelDef=paste(' ', SelDef, #1)", NULL)
PgmOptArg("name=var", NULL, "Selektionsparameter")

msgtab {
	1, "$!: Datei $(Input) ist keine Datenmatrix.\n"
}

loadarg;

if	(locale)	setlocale(LC_ALL, locale);

mdmat md;

if	(SCFlag)
{
	str name = tempnam();
	IO io = open(sprintf("|sc %s >/dev/null", Input), "w");
	fprintf(io, "T%s\nq", name);
	close(io);
	md = mdread(open(name, "r"), Flags);
	remove(name);
}
else	md = mdread(open(Input, "rz"), Flags);

if	(md == NULL)	error(NULL, 1);

if	(SelDef || VarDef)	md = reload(md, SelDef, VarDef);

if	(Reduce)	md.reduce();
if	(Title)		md.title = Title;

if	(PrintMode)	md.mark("#-1").print(iostd);
else			md.save(Output);

free(md);
