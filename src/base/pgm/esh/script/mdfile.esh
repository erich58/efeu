Ident = "Informationen zu einer Datenmatrix ausgeben";
Copyright = "(c) 1997 Erich Frühstück, A-1090 Wien, Währinger Straße 64/6";
Version = "2";

bool ListAxis = false;
bool AllStat = false;

PgmOpt("a", "AllStat=true", "Status zu allen Dateien")
PgmOpt("x", "ListAxis=true", "Achsen auflisten")
PgmVaArg("file(s)", NULL, "Datenfile(s)")

void list_axis(mdmat md)
{
	mdaxis x;

	if	(!ListAxis)	return;

	for (x = md.axis; x != NULL; x = x.next)
	{
		int n = printf("%s:", x.name);

		for (y in md.label(x.name))
		{
			if	(n > 70)
			{
				iostd << "\n\t";
				n = 8 + printf("%s", y);
			}
			else	n += printf(" %s", y);
		}

		iostd << "\n";
	}
}

loadarg;

int i;
IO io;

for (i = 1; i < argc; i++)
{
	str name;
	IO io;

	if	(argv[i] == "-")
	{
		io = iostd;
		name = "<stdin>";
	}
	else	io = open(name = argv[i], "rz");

	mdmat md = mdread(io, "magic");
	close(io);

	if	(md)
	{
		mdaxis x;
		str delim;

		printf("%s: %s[%d",
			name, (str) md.type, md.size / sizeof(md.type));

		delim = "=";

		for (x = md.axis; x != NULL; x = x.next)
		{
			printf("%s%d", delim, x.dim);
			delim = "*";
		}

		printf("] %#s\n", md.title);
		list_axis(md);
		free(md);
	}
	else if	(AllStat)
	{
		printf("%s: Keine Datenmatrix.\n", name);
	}
}
