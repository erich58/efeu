/*
:*:	show structure of data cube
:de:	Informationen zu einer Datenmatrix ausgeben

Copyright (C) 1997, 2001 Erich Frühstück
This file is part of EFEU.

EFEU is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public
License as published by the Free Software Foundation; either
version 2 of the License, or (at your option) any later version.

EFEU is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty
of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with EFEU; see the file COPYING.
If not, write to the Free Software Foundation, Inc.,
59 Temple Place, Suite 330, Boston, MA 02111-1307, USA.
*/

pconfig !
#if	Lang == "de"
Ident = "Informationen zu einer Datenmatrix ausgeben"
#else
Ident = "show structure of data cube"
#endif
Version = "2"
Copyright = "(c) 1997, 2001 Erich Frühstück"
AllStat = false
ListAxis = false

a | AllStat = true
	:*:status of all files
	:de:Status zu allen Dateien
x | ListAxis = true
	:*:show labels
	:de:Achsen auflisten
*file(s) | 
	:*:files
	:de:Datenfile(s)
!

void list_axis(mdmat md)
{
	mdaxis x;

	if	(!flagres("ListAxis"))	return;

	for (x = md.axis; x != NULL; x = x.next)
	{
		int n = printf("%s:", x.name);

		for (y in md.label(x.name))
		{
			if	(n > 70)
			{
				iostd << "\n\t";
				n = 8 + printf("%s", y);
			}
			else	n += printf(" %s", y);
		}

		iostd << "\n";
	}
}

int i;
IO io;

for (i = 1; i < argc; i++)
{
	str name;
	IO io;

	if	(argv[i] == "-")
	{
		io = iostd;
		name = "<stdin>";
	}
	else	io = open(name = argv[i], "rz");

	mdmat md = mdread(io, "magic");
	close(io);

	if	(md)
	{
		mdaxis x;
		str delim;

		printf("%s: %s[%d",
			name, (str) md.type, md.size / sizeof(md.type));

		delim = "=";

		for (x = md.axis; x != NULL; x = x.next)
		{
			printf("%s%d", delim, x.dim);
			delim = "*";
		}

		printf("] %#s\n", md.title);
		list_axis(md);
		free(md);
	}
	else if	(flagres("AllStat"))
	{
		printf("%s: Keine Datenmatrix.\n", name);
	}
}
