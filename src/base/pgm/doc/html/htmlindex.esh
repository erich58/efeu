/*
:*:create index.html for current directory
:de:HTML-Index in der aktuellen Bibliothek generieren

$Copyright (C) 2001 Erich Frühstück
This file is part of EFEU.

EFEU is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public
License as published by the Free Software Foundation; either
version 2 of the License, or (at your option) any later version.

EFEU is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty
of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with EFEU; see the file COPYING.
If not, write to the Free Software Foundation, Inc.,
59 Temple Place, Suite 330, Boston, MA 02111-1307, USA.
*/

pconfig !
Output = "index.html";
MAGIC = "HTMLINDEX";

::name|Output
	:*:output file
	:de:Ausgabefile
!

List_t list = flist(".", "main.html");

if	(dim(list) == 0)
{
	fpsub(ioerr, langcpy(string !
:*:$!: no html-documents found!
:de:$!: Keine HTML-Dokumente gefunden!
!));
	exit(0);
}

if	(fsearch(NULL, Output))
{
	IO io = open(Output, "r");

	if	(!regexec(MAGIC, fgets(io, EOF)))
	{
		fpsub(ioerr, langcpy(string !
:*:$!: file $(Output) not created from $!!
:de:$!: Datei $(Output) wurde nicht von $! generiert!
!));
		exit(0);
	}
}

IO out = open(Output, "w");

RegExp regex = "<TITLE>(.*)</TITLE>";

struct Entry {
	str name;
	str title;
};

int CmpEntry (Entry a, Entry b) lexcmp(a.title, b.title)

DataBase db = DataBase(Entry);

for (x in list)
{
	str head = filter("sed -e '/<\/TITLE>/q' " + x);
	db += { substr(x, 2), regex.match(head).next.obj };
}

db.sort(CmpEntry);

fpsub(out, string !
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML>
<!-- CREATOR: $(MAGIC:%s) -->
<HEAD>
#if	Lang == "de"
<TITLE>Dokumentindex</TITLE>
#else
<TITLE>document index</TITLE>
#endif
</HEAD>
<BODY>
!);

for (x in db)
	fprintf(out, "<A HREF=%#s> %s<A><BR>\n", x.name, x.title);

out << string !
</BODY>
</HTML>
!;
