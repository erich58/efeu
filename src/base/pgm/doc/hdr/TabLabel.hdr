/*	Labeldefinitionen
	(c) 1996, 2009 Erich Frühstück
*/

#ifndef	_include_TabLabel_hdr
#define	_include_TabLabel_hdr	1

struct TabLabelIndex {
	int num;	/* Nummer */
	str name;	/* Kennung */
	str label;	/* Bezeichnung */
};

TabLabelIndex int ()
	return { this };

virtual int fprint(IO io, TabLabelIndex idx)
	fprintf(io, "\t%d: %#s, %#s\n", num, name, label)

struct TabLabel {
	str key;	/* Kennung */
	str name;	/* Achsenname */
	str list;	/* Selektionsliste */
	TabLabelIndex[] tab;	/* Indextabelle */
	str prefix;	/* Vorspann */
	str special;	/* Sonderdefinitionen */
};

int CompLabel(TabLabel a, TabLabel b)
	cmp(a.key, b.key)

bool TabLabel ()
	this.key ? true : false;

str TabLabel ()
	paste("=", this.name, this.list)

TabLabel _Ptr_ ()
	return { NULL };

virtual int fprint(IO io, TabLabel label)
{
	if	(label.key == NULL)
		return fprint(io, "NULL");

	int n = fprintf(io, "%s[%s] = {\n", label.key, (str) label);

	for (x in label.tab)
		fprintf(io, "\t%s, %#s\n", x.name, x.label)

	return n + fprintf(io, "}\n");
}


/*	Konstruktionshilfsfunktionen
*/

void TabLabel::entry (str key, str label)
{
	if	(key[0] == '@')
	{
		if	(key[1] == '+')	prefix += '\t';

		prefix += label;

		if	(key[1] != '*')	prefix += '\n';

		return;
	}

	if	(name && key != ".")
	{
		list = paste(",", list, key);

		if	(key[0] == ':')
			key = split(key, ":[").next.obj;

		key = paste("=", name, key);
	}
	else	key = NULL;

	int width;
	str x;

	tab.append({ dim(tab), key, label });
}

TabLabel TabLabel(str name, str axis)
	return { name, axis };

TabLabel TabLabel(str name, str axis, promotion IO io)
{
	str key, label;

	TabLabel x = { name, axis };

	while (getline(io, key, label))
		x.entry(key, label);

	return x;
}

/*	Dummylabel
*/

TabLabel CDummyTabLabel = TabLabel("CDummy", NULL, string !
.	~
A	Spalte A
B	Spalte B
C	Spalte C
D	Spalte D
!);

TabLabel LDummyTabLabel = TabLabel("LDummy", NULL, string !
1	Zeile 1
2	Zeile 2
3	Zeile 3
4	Zeile 4
.	.......
n	Zeile n
!);

#endif	/* _include_TabLabel_hdr */
